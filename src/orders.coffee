PDF = require "pdfkit"
moment = require "moment"
pharmacies = require "./pharmacies"

generateOrderNotification = (output, pharmacyId, date, lowDrugs, callback) ->
  pharmacies.getPharmacy id: pharmacyId, (status, pharmacy) ->
    pdf = new PDF
    pdf.pipe output

    buildHeader pdf, pharmacy, date
    buildContent pdf, lowDrugs
    buildFooter pdf

    pdf.end()
    callback null


buildHeader = (pdf, pharmacy, date) ->
  headerLeft = 60
  headerCenter = 250
  headerLineStart = 50
  headerLineHeight = 20

  pdf.fontSize 14
  pdf.text "Order Notification", headerLeft, headerLineStart
  pdf.text "Date: #{moment(date, 'YYYYMMDD').format('YYYY-MM-DD')}", headerCenter, headerLineStart, align: 'right'
  pdf.fontSize 12
  pdf.text "Pharmacy: #{pharmacy.name ? ''}", headerLeft, headerLineStart + 2*headerLineHeight
  pdf.text "Pharmacy Id: #{pharmacy.pharmacyId}", headerCenter, headerLineStart + 2*headerLineHeight, align: 'right'
  pdf.text "Location: #{pharmacy.location ? ''}", headerLeft, headerLineStart + 3*headerLineHeight
  pdf.rect(headerLeft-10, headerLineStart-10, 500, 90).stroke()

buildContent = (pdf, drugs) ->
  drugsCol0 = 60
  drugsCol1 = drugsCol0+120
  drugsCol2 = drugsCol1+75
  drugsCol3 = drugsCol2+95
  drugsCol4 = drugsCol3+90
  drugsLineStart = 180
  drugsLineHeight = 15

  curLine = drugsLineStart

  pdf.fontSize 10
  pdf.text "Name", drugsCol0, curLine
  pdf.text "Code Type", drugsCol1, curLine
  pdf.text "Code", drugsCol2, curLine
  pdf.text "Stock Level", drugsCol3, curLine
  pdf.text "Avg Daily Dispensed", drugsCol4, curLine
  pdf.moveTo(drugsCol0, curLine + drugsLineHeight*1.4)
    .lineTo(535, curLine + drugsLineHeight*1.4)
    .stroke()
  curLine += drugsLineHeight
  for drug in drugs
    curLine += drugsLineHeight
    pdf.text "#{if drug.name.length > 20 then drug.name[0..20] + "..." else drug.name}", drugsCol0, curLine
    pdf.text "#{drug.identifiers[0]?.drugCodeType}", drugsCol1, curLine
    pdf.text "#{drug.identifiers[0]?.drugCode}", drugsCol2, curLine
    pdf.text "#{drug.level}", drugsCol3, curLine
    pdf.text "#{drug.avgDailyDispensed?.toFixed 1}", drugsCol4, curLine

buildFooter = (pdf) ->
  pdf.fontSize 8
  pdf.text "Generated by the Drug Stock Warehouse on #{moment().format('YYYY-MM-DD HH:mm:ss')}", 100, 695, align: 'right'
  pdf.text "Cell-Life NPC & Jembi Health Systems NPC", 100, 710, align: 'right'

exports.generateOrderNotification = generateOrderNotification
